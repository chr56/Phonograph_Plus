apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

Boolean signing_file_exist = rootProject.file("signing.properties").exists()
def signingProperties = new Properties()
if (signing_file_exist) {
    signingProperties.load(new FileInputStream(rootProject.file("signing.properties")))
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 30

        renderscriptTargetApi 29
        vectorDrawables.useSupportLibrary = true

        applicationId 'com.kabouzeid.gramophone'
        versionCode 184
        versionName '1.3.5'
        versionNameSuffix '-' + getGitHash()
    }
    signingConfigs {
        release {
            if (signing_file_exist) {
            storeFile file(signingProperties['storeFile'])
            storePassword signingProperties['storePassword']
            keyAlias signingProperties['keyAlias']
            keyPassword signingProperties['keyPassword']
            }
        }
        ci_trustless {
            storeFile rootProject.file("public_signing_key.jks")
            keyAlias 'trustless'
            storePassword '000000'
            keyPassword '000000'
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if(signing_file_exist) signingConfig signingConfigs.release
            resValue "string", "app_name","Phonograph"
        }
        cust{
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if(signing_file_exist) signingConfig signingConfigs.release
            resValue "string", "app_name","Phonograph Custom"
            applicationIdSuffix '.cust'
            matchingFallbacks = ['release']
            debuggable true
        }
        experimental{
            if(signing_file_exist) signingConfig signingConfigs.release
            resValue "string", "app_name","Phonograph Experi"
            applicationIdSuffix '.experimental'
            matchingFallbacks = ['release']
            debuggable true
        }
        pub{
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if(signing_file_exist) signingConfig signingConfigs.release
            resValue "string", "app_name","Phonograph Pro"
            applicationIdSuffix '.mod'
            matchingFallbacks = ['release']
        }
        debug {
            applicationIdSuffix '.debug'
            resValue "string", "app_name","Phonograph Debug"
        }
        // for ci
        ci {
            signingConfig signingConfigs.ci_trustless
            applicationIdSuffix '.ci'
            resValue "string", "app_name","Phonograph CI build"
            matchingFallbacks = ['release']
            debuggable true
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    lintOptions {
        disable 'MissingTranslation'
        disable 'InvalidPackage'
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    viewBinding {
        enabled = true
    }
}

dependencies {
    implementation project(path: ':MDthemer')

    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.cardview:cardview:1.0.0'
//    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.core:core:1.6.0'
    implementation 'androidx.fragment:fragment:1.3.6'
    implementation 'androidx.media:media:1.4.2'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.4.0'

    //implementation 'com.github.kabouzeid:app-theme-helper:1.3.10'
    implementation 'com.github.kabouzeid:RecyclerView-FastScroll:1.0.16-kmod'
    implementation 'com.github.kabouzeid:SeekArc:1.2-kmod'
    implementation 'com.github.kabouzeid:AndroidSlidingUpPanel:6'
    implementation 'com.afollestad.material-dialogs:core:3.3.0'
    implementation 'com.afollestad.material-dialogs:input:3.3.0'
    implementation 'com.afollestad.material-dialogs:color:3.3.0'
    implementation 'com.afollestad.material-dialogs:files:3.3.0'


    implementation 'com.afollestad:material-cab:0.1.12'
    implementation 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    implementation 'com.heinrichreimersoftware:material-intro:2.0.0'
    //implementation 'me.zhanghai.android.materialprogressbar:library:1.6.1'
    implementation('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.11.0@aar') {
        transitive = true
    }

    implementation 'com.jakewharton:butterknife:10.2.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.1'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.google.code.gson:gson:2.8.6'

    implementation 'de.psdev.licensesdialog:licensesdialog:2.1.0'

    implementation 'com.github.bumptech.glide:glide:3.8.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:1.5.0'
    implementation 'org.eclipse.mylyn.github:org.eclipse.egit.github.core:2.1.5'

    implementation 'com.github.AdrienPoupa:jaudiotagger:2.2.3'
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}
